plugins {
    id 'java'
    id 'idea'
    id 'com.github.ben-manes.versions' version '0.27.0'
}

group 'uk.dioxic'
version '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    log4jVersion = '2.13.0'
    jacksonVersion = '2.10.2'
    picocliVersion = '4.1.4'
    junitVersion = '5.6.0'
    mgenVersion = '0.0.6-SNAPSHOT'
    reactorVersion = '3.3.2.RELEASE'
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:1.18.10"
    compileOnly "org.projectlombok:lombok:1.18.10"
    implementation "org.mongodb:mongodb-driver-reactivestreams:1.12.0"
    implementation "org.mongodb:mongodb-driver-sync:3.11.0"
    implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
    implementation "org.apache.commons:commons-lang3:3.9"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "io.projectreactor:reactor-core:$reactorVersion"
    implementation "uk.dioxic.mgenerate:mgenerate-core:$mgenVersion"
    implementation "info.picocli:picocli:$picocliVersion"
    testImplementation "io.projectreactor:reactor-test:$reactorVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.assertj:assertj-core:3.14.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    annotationProcessor "info.picocli:picocli-codegen:$picocliVersion"
    annotationProcessor "uk.dioxic.mgenerate:mgenerate-apt:$mgenVersion"
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

test {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        showStandardStreams = true
    }
}

jar {
    //archiveClassifier = 'uber'
    archiveName 'mongo-tdk.jar'

    manifest {
        attributes "Main-Class": "uk.dioxic.mtdk.Main"
    }

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}
